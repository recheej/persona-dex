apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.persona5dex"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 9
        versionName "1.2.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            buildConfigField("boolean", "ENABLE_CRASHLYTICS", "true")
            resValue("string", "name_provider_auth", defaultConfig.applicationId + ".personanameprovider")
        }
        debug {
            buildConfigField("boolean", "ENABLE_CRASHLYTICS", "false")
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue("string", "name_provider_auth", defaultConfig.applicationId + applicationIdSuffix + ".personanameprovider")
        }
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:26.1.0'
    compile 'com.google.dagger:dagger:2.11'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'net.cachapa.expandablelayout:expandablelayout:2.9.1'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    compile 'com.github.myinnos:AlphabetIndex-Fast-Scroll-RecyclerView:1.0.8'
    testCompile 'org.robolectric:robolectric:3.4.2'
    compile 'com.github.medyo:android-about-page:1.2.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.7.0@aar') {
        transitive = true;
    }
}
