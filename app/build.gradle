apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.persona5dex"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 16
        versionName "2.2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            buildConfigField("boolean", "ENABLE_CRASHLYTICS", "true")
            resValue("string", "name_provider_auth", defaultConfig.applicationId + ".personanameprovider")
            buildConfigField("int", "FUSION_VERSION_NUMBER", "1")
        }
        debug {
            buildConfigField("boolean", "ENABLE_CRASHLYTICS", "false")
            buildConfigField("int", "FUSION_VERSION_NUMBER", "1")
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue("string", "name_provider_auth", defaultConfig.applicationId + applicationIdSuffix + ".personanameprovider")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.firebase:firebase-auth:16.0.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.google.dagger:dagger:2.11'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.1'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    implementation 'com.github.myinnos:AlphabetIndex-Fast-Scroll-RecyclerView:1.0.8'
    testImplementation 'org.robolectric:robolectric:3.6.1'
    implementation 'com.github.medyo:android-about-page:1.2.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.0@aar') {
        transitive = true
    }
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'com.github.humazed:RoomAsset:v1.0'
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    implementation 'com.android.support:preference-v7:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    androidTestImplementation 'android.arch.persistence.room:testing:1.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-storage:16.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
